|| включить "kspi/kspi"
|| включить "k-serial/kserial"
|| включить "k-collections/kcollections"

алг получить веб запрос(рез лит __метод, рез лит __путь, рез лит __запрос, рез лит __заголовки, рез лит __тело) нач
    лит тип, _тело
    считать кппи сообщение(тип, _тело)
    цел индекс
    лит метод = "", путь = "", запрос = "", заголовки = "", тело = "", текст = ""
    лит стадия = "метод"
    нц для индекс от 1 до длин(_тело)
        если стадия = "конец" то выход все
        если _тело[индекс] = нс то
            выбор
                при стадия = "метод":
                    метод := текст
                    текст := ""
                    стадия := "путь"
                    |журнал("инфо", метод)
                при стадия = "путь":
                    путь := текст
                    текст := ""
                    стадия := "запрос"
                    |журнал("инфо", путь)
                при стадия = "запрос":
                    запрос := текст
                    текст := ""
                    стадия := "заголовки"
                    |журнал("инфо", запрос)
                при стадия = "заголовки":
                    заголовки := текст
                    текст := ""
                    стадия := "тело"
                при стадия = "тело":
                    тело := текст
                    стадия := "конец"
            все
        иначе
            текст := текст + _тело[индекс]
        все
    кц
    тело := текст
    __метод := метод
    __путь := путь
    __запрос := запрос
    __заголовки := заголовки
    __тело := десериализовать(тело)
кон


алг послать веб ответ(арг лит статускод, арг лит заголовки, арг лит тело) нач
    лит ответ = ""
    ответ := ответ + статускод + нс
    лит ключи, значения
    разобратьОбъект(заголовки, ключи, значения)
    цел индекс
    нц для индекс от 1 до длинаКоллекции(ключи)
        ответ := ответ + получитьИзСписка(ключи, индекс) + ":" + получитьИзСписка(значения, индекс) + нс
    кц
    ответ := ответ + "---" + нс + тело
    послать кппи сообщение("web_ans", ответ)
кон
