|
| Kumir Serial Python Interface
| @author Tapeline
| @version 1
|

алг считать кппи сообщение (рез лит __тип, рез лит __тело) нач
    лит заголовок
    |журнал("инфо", "Читаем сообщение")
    ввод заголовок
    |журнал("инфо", заголовок)
    лит тип = "", литдлин = "", тело = ""
    лит состояние = "тип"
    цел индекс
    нц для индекс от 1 до длин(заголовок)
        если состояние = "тело" то выход все
        если заголовок[индекс] = ';' то
            выбор
            при состояние = "тип":
                состояние := "длин"
                |журнал("инфо", "Тип прочитан")
                |журнал("инфо",тип)
            при состояние = "длин":
                состояние := "тело"
                |журнал("инфо", "Длина прочитана")
                |журнал("инфо",литдлин)
            все
        иначе
            выбор
            при состояние = "тип": тип := тип + заголовок[индекс]
            при состояние = "длин": литдлин := литдлин + заголовок[индекс]
            все
        все
    кц
    |журнал("инфо", "Дочитка тела")
    тело := заголовок[индекс:длин(заголовок)] + нс
    | Дочитать тело
    цел длина тела
    лог ошибка
    длина тела := лит_в_цел(литдлин, ошибка)
    индекс := длин(тело)
    лит строка
    нц пока индекс < длина тела
        |журнал("инфо",цел_в_лит(индекс))
        ввод строка
        тело := тело + строка + нс
        индекс := длин(тело)
    кц
    __тело := тело[1:длин(тело)-1]  | убрать последний LF
    __тип := тип
кон


алг послать кппи сообщение (арг лит тип, арг лит тело) нач
    вывод тип, ";", цел_в_лит(длин(тело)), ";", тело, нс
кон


алг журнал(арг лит __уровень, арг лит сообщение) нач
    лит уровень = "INFO"
    выбор
        при __уровень = "ошибка": уровень := "ERROR"
        при __уровень = "предупр": уровень := "WARNING"
    все
    послать кппи сообщение("log", уровень + ";" + сообщение)
кон
